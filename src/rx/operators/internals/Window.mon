/* 
* Copyright (c) 2018 Software AG, Darmstadt, Germany and/or its licensors
*
* SPDX-License-Identifier: Apache-2.0
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.ISubject;

/** @private */
event WindowSubscriberFactory {
	IObserver subscriber;
	IObserver upstream;
	IObservable triggeringSource;
	optional<ISubject> currentSubject;
	
	static action create(IObserver subscriber, IObservable observable) returns WindowSubscriberFactory {
		WindowSubscriberFactory w := WindowSubscriberFactory(subscriber, new IObserver, observable, new optional<ISubject>);
		w.createNextSubject();
		return w;
	}
	
	action next(any value) {
		currentSubject.getOrThrow().next(value);
	}
	
	action complete() {
		currentSubject.getOrThrow().complete();
		subscriber.complete();
	}
	
	action createNextSubject() {
		ifpresent currentSubject {
			currentSubject.complete();
		}
		currentSubject := (<action<> returns ISubject> any.newInstance("com.industry.rx_epl.Subject").getAction("create"))();
		subscriber.next(currentSubject.getOrThrow().asIObservable());
	}
	
	action triggerNext(any value) {
		createNextSubject();
	}
	
	action connectTrigger() {
		ISubscription s := triggeringSource.connectObserver(IObserverBuilder.create(subscriber.createUpstream()).onNext(triggerNext).build());
	}
	
	action getUpstream() returns IObserver {
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(next).onComplete(complete).build();
	}
}

/** @private */
event WindowOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	IObservable observable;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, IObservable observable) returns action<IObserver> returns ISubscription {
		return WindowOnConnection(parentOnConnection, observable).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		WindowSubscriberFactory factory := WindowSubscriberFactory.create(subscriber, observable);
		ISubscription s := parentOnConnection(factory.getUpstream());
		if subscriber.subscribed() {
			factory.connectTrigger();
		}
		return s;
	}
}