/* 
* Copyright 2018 Software AG
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.utils.Actions;

/** @private */
event EverySubscriber {
	IObserver subscriber;
	action<sequence<any> > returns any predicate;
	
	static action create(IObserver subscriber, action<sequence<any> > returns any predicate) returns IObserver {
		EverySubscriber a := EverySubscriber(subscriber, predicate);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(a.next).onComplete(a.complete).build();
	}
	
	action next(any value) {
		try {
			if not <boolean> predicate([value]) {
				subscriber.next(false);
				subscriber.complete();
			}
		} catch(Exception e) {
			subscriber.error(e);
		}
	}
	
	action complete() {
		subscriber.next(true);
		subscriber.complete();
	}
}

/** @private */
event EveryOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	action<sequence<any> > returns any predicate;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, any predicate) returns action<IObserver> returns ISubscription {
		Actions.checkAction(predicate, 1, 0, "boolean");
		return EveryOnConnection(parentOnConnection, Actions.getCoercingGenericAction(predicate)).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(EverySubscriber.create(subscriber, predicate));
	}
}
