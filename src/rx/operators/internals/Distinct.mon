/* 
* Copyright (c) 2018 Software AG, Darmstadt, Germany and/or its licensors
*
* SPDX-License-Identifier: Apache-2.0
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.utils.Actions;

/** @private */
event DistinctBySubscriber {
	IObserver subscriber;
	action<sequence<any> > returns any distinctBy;
	dictionary<any, boolean> valuesSeen;
	
	static action create(IObserver subscriber, action<sequence<any> > returns any distinctBy) returns IObserver {
		DistinctBySubscriber m := DistinctBySubscriber(subscriber, distinctBy, new dictionary<any, boolean>);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).build();
	}
	
	action next(any value) {
		try {
			any key := value;
			ifpresent distinctBy {
				key := distinctBy([value]);
			}
			if not valuesSeen.hasKey(key) {
				valuesSeen.add(key, true);
				subscriber.next(value);
			}
		} catch(Exception e) {
			subscriber.error(e);
		}	
	}
}

/** @private */
event DistinctByOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	action<sequence<any> > returns any distinctBy;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, any distinctBy) returns action<IObserver> returns ISubscription {
		action<sequence<any> > returns any genericAction;
		ifpresent distinctBy {
			Actions.checkAction(distinctBy, 1, 0, "any");
			genericAction := Actions.getCoercingGenericAction(distinctBy, "any");
		} else {
			genericAction := new action<sequence<any> > returns any;
		}
		return DistinctByOnConnection(parentOnConnection, genericAction).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		IObserver mapSubscriber := DistinctBySubscriber.create(subscriber, distinctBy);
		return parentOnConnection(mapSubscriber);
	}
}