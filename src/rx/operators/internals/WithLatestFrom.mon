/* 
* Copyright 2018 Software AG
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;

/** @private */
event WithLatestFromSubscriberFactory {
	IObserver downstreamSubscriber;
	integer subscriberCount;
	integer emittingId;
	
	dictionary<integer, any> latestValuesById;
	dictionary<integer, IObserver> upstreamSubscribersById;
	dictionary<integer, boolean> completeSubscribersById;
	
	static action create(IObserver subscriber) returns WithLatestFromSubscriberFactory {
		return WithLatestFromSubscriberFactory(subscriber, 0, 0, new dictionary<integer, any>, new dictionary<integer, IObserver>, new dictionary<integer, boolean>);
	}
	
	action createUpstreamSubscriber(boolean causesEmission) returns IObserver {
		subscriberCount := subscriberCount + 1;
		integer id;
		if causesEmission {
			id := 0;
		} else {
			id := subscriberCount;
		}
		IObserver o := Subscriber.create().onNext(CombineLatestOnNext.create(id, next)).onError(downstreamSubscriber.error).onComplete(CombineLatestOnComplete.create(id, complete)).asIObserver();
		downstreamSubscriber.onUnsubscribe(o.unsubscribe);
		upstreamSubscribersById.add(id, o);
		return o;
	}
	
	action next(integer id, any value) {
		latestValuesById.add(id, value);
		if id = emittingId and latestValuesById.size() = subscriberCount {
			// These are automatically sorted in ascending order of keys
			sequence<any> bugFix := new sequence<any>;
			bugFix.appendSequence(latestValuesById.values());
			downstreamSubscriber.next(bugFix);
		}
	}
	
	action complete(integer id) {
		completeSubscribersById.add(id, true);
		if id = emittingId {
			downstreamSubscriber.complete();
		}
		upstreamSubscribersById[id].unsubscribe();
	}
}

/** @private */
event WithLatestFromOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	sequence<IObservable> otherObservables;
	boolean receivedValue;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, sequence<IObservable> otherObservables) returns action<IObserver> returns ISubscription {
		return WithLatestFromOnConnection(parentOnConnection, otherObservables, true).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		WithLatestFromSubscriberFactory factory := WithLatestFromSubscriberFactory.create(subscriber);
		
		sequence<IObserver> upstreamSubscribers := new sequence<IObserver>;
		
		IObservable observable;
		for observable in otherObservables {
			upstreamSubscribers.append(factory.createUpstreamSubscriber(false));
		}
		IObserver emittingUpstream := factory.createUpstreamSubscriber(true);

		ISubscription s := Subscription.create(subscriber).asISubscription();
		
		integer i := 0;
		while i < upstreamSubscribers.size() {
			IObserver upstream := upstreamSubscribers[i];
			ISubscription upstreamSubscription := otherObservables[i].connectObserver(upstream);
			i := i + 1;
		}
		// Make sure that the emissionCausingObservable is the last one to connect
		ISubscription upstreamSubscription := parentOnConnection(emittingUpstream);
		
		return s;
	}
}