/* 
* Copyright 2018 Software AG
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;

/** @private */
event TimeoutSubscriber {
	IObserver subscriber;
	float timeout;
	listener timeoutListener;
	
	static action create(IObserver subscriber, float timeout) returns IObserver {
		TimeoutSubscriber m := TimeoutSubscriber(subscriber, timeout, new listener);
		subscriber.onUnsubscribe(m.quitTimeoutListener);
		m.updateListener();
		return IObserverBuilder.create(subscriber.createUpstream()).onComplete(m.quitTimeoutListener).onNext(m.next).build();
	}
    
	action next(any value) {
		updateListener();
		subscriber.next(value);
	}
	
	action updateListener() {
		quitTimeoutListener();
		timeoutListener := on wait(timeout) {
			subscriber.error(Exception("Timed out after " + timeout.toString() + " seconds", "TimeoutException"));
		}
	}
	
	action quitTimeoutListener() {
		ifpresent timeoutListener {
			timeoutListener.quit();
		}		
	}
}

/** @private */
event TimeoutOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	float timeout;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, float timeout) returns action<IObserver> returns ISubscription {
		return TimeoutOnConnection(parentOnConnection, timeout).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		return parentOnConnection(TimeoutSubscriber.create(subscriber, timeout));
	}
}