/* 
* Copyright 2018 Software AG
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;

/** @private */
event SkipUntilSubscriber {
	IObserver subscriber;
	action<any> _next;
	
	static action create(IObserver subscriber, IObservable until) returns IObserver {
		SkipUntilSubscriber m := SkipUntilSubscriber(subscriber, new action<any>);
		ISubscription untilSubscription := until.take(1).subscribe(Subscriber.create().onComplete(m.untilComplete));
		subscriber.onUnsubscribe(untilSubscription.unsubscribe);
		return IObserverBuilder.create(subscriber.createUpstream()).onNext(m.next).onComplete(m.complete).build();
	}
	
	action next(any value) {
		ifpresent _next {
			_next(value);
		}
	}
	
	action complete() {
		subscriber.complete();
	}
	
	action untilComplete() {
		_next := subscriber.next;
	}
}

/** @private */
event SkipUntilOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	IObservable until;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection, IObservable until) returns action<IObserver> returns ISubscription {
		return SkipUntilOnConnection(parentOnConnection, until).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		IObserver upstreamSubscriber := SkipUntilSubscriber.create(subscriber, until);
		return parentOnConnection(upstreamSubscriber);
	}
}