/* 
* Copyright 2018 Software AG
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
package com.industry.rx_epl.operators.internals;

using com.apama.exceptions.Exception;

using com.industry.rx_epl.IObserver;
using com.industry.rx_epl.IObserverBuilder;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.ISubscription;
using com.industry.rx_epl.Subscription;
using com.industry.rx_epl.Subscriber;
using com.industry.rx_epl.utils.Utils;

/** @private */
event CombineLatestCombinerMapper {
	action<sequence<any> > returns any combiner;
	
	static action create(action<sequence<any> > returns any combiner) returns action<any> returns any {
		return CombineLatestCombinerMapper(combiner).mapper;
	}
	
	action mapper(any value) returns any {
		return combiner(<sequence<any> > value);
	}
}

/** @private */
event CombineLatestOnNext {
	integer id;
	action<integer, any> onNext;
	
	static action create(integer id, action<integer, any> onNext) returns action<any> {
		return CombineLatestOnNext(id, onNext).next;
	}
	
	action next(any value) {
		onNext(id, value);
	}
}

/** @private */
event CombineLatestOnComplete {
	integer id;
	action<integer> onComplete;
	
	static action create(integer id, action<integer> onComplete) returns action<> {
		return CombineLatestOnComplete(id, onComplete).complete;
	}
	
	action complete() {
		onComplete(id);
	}
}

/** @private */
event CombineLatestSubscriberFactory {
	IObserver downstreamSubscriber;
	integer subscriberCount;
	
	dictionary<integer, any> latestValuesById;
	dictionary<integer, IObserver> upstreamSubscribersById;
	dictionary<integer, boolean> completeSubscribersById;
	
	static action create(IObserver subscriber) returns CombineLatestSubscriberFactory {
		return CombineLatestSubscriberFactory(subscriber, 0, new dictionary<integer, any>, new dictionary<integer, IObserver>, new dictionary<integer, boolean>);
	}
	
	action createUpstreamSubscriber() returns IObserver {
		integer id := subscriberCount;
		subscriberCount := subscriberCount + 1;
		IObserver o := Subscriber.create().onNext(CombineLatestOnNext.create(id, next)).onError(downstreamSubscriber.error).onComplete(CombineLatestOnComplete.create(id, complete)).asIObserver();
		downstreamSubscriber.onUnsubscribe(o.unsubscribe);
		upstreamSubscribersById.add(id, o);
		return o;
	}
	
	action next(integer id, any value) {
		latestValuesById.add(id, value);
		if latestValuesById.size() = subscriberCount {
			// These are automatically sorted in ascending order of keys
			downstreamSubscriber.next(latestValuesById.values());
		}
	}
	
	action complete(integer id) {
		completeSubscribersById.add(id, true);
		if completeSubscribersById.size() = subscriberCount {
			downstreamSubscriber.complete();
		}
		upstreamSubscribersById[id].unsubscribe();
	}
}

/** @private */
event CombineLatestOnConnection {
	action<IObserver> returns ISubscription parentOnConnection;
	sequence<IObservable> observables;
	
	static action create(action<IObserver> returns ISubscription parentOnConnection,  sequence<IObservable> observables) returns action<IObserver> returns ISubscription {
		return CombineLatestOnConnection(parentOnConnection, observables).onConnection;
	}
	
	action onConnection(IObserver subscriber) returns ISubscription {
		CombineLatestSubscriberFactory factory := CombineLatestSubscriberFactory.create(subscriber);
		
		IObserver mainUpstreamObserver := factory.createUpstreamSubscriber();
		sequence<IObserver> upstreamSubscribers := new sequence<IObserver>;
		
		IObservable observable;
		for observable in observables {
			upstreamSubscribers.append(factory.createUpstreamSubscriber());
		}
		
		ISubscription s := Subscription.create(subscriber).asISubscription();
		
		// Connect the main observable
		ISubscription firstConnection := parentOnConnection(mainUpstreamObserver);
		
		// Connect the others
		integer i := 0;
		while i < upstreamSubscribers.size() {
			IObserver upstream := upstreamSubscribers[i];
			ISubscription upstreamSubscription := observables[i].connectObserver(upstream);
			i := i + 1;
		}
		
		return s;
	}
}