using utils.TestObservables;
using utils.Test;
using utils.ExpectValues;

using com.industry.rx_epl.Observable;
using com.industry.rx_epl.IResolver;
using com.industry.rx_epl.Subscriber;

monitor TestObservable {
	Test test := Test("TestResult");

	integer teardownCount := 0;

	action onload() {
		context testContext := context("NewTestContext", false);

	    any discard := Observable.create(generator)
			.do(Subscriber.create().onNext(checkContextNext).onComplete(checkContext))
			.take(4)
			.do(Subscriber.create().onNext(checkContextNext).onComplete(checkContext))
			.do(ExpectValues.create([<any>"Value", "Value", "Value", "Value"], checkContext, test.fail))
	      	.subscribeOn(Subscriber.create().onNext(checkContextNext).onComplete(checkContextComplete), testContext);
	}

	action generator(IResolver resolver) {
		checkContext();
		on all wait(0.1) and not wait(0.5) {
			resolver.next("Value");
		}
		on wait(0.5) {
			resolver.complete();
		}
	}

	action checkContextNext(any value) {
		checkContext();
	}

	action checkContextComplete() {
		checkContext();
		test.complete();
	}

	action checkContext() {
		if context.current().getName() != "NewTestContext" {
			test.fail("Running on wrong context");
		}
	}
}
