using utils.TestObservables;
using utils.Test;
using utils.ExpectValues;

using com.industry.rx_epl.Observable;
using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.IDisposable;
using com.industry.rx_epl.Subscriber;

monitor TestObservable {
	Test test := Test("TestResult");
  
	boolean isConnected := false;
	boolean isDisposed := false;
	
	action onload() {
	     IObservable connectableObservable := Observable.interval(0.1)
                                                      .map(makeSureNotCalledUntilConnected)
                                                      .publish();
	    
	     any subscription := connectableObservable.subscribe(ExpectValues.createNoComplete([<any>0,1,2,3,4],onTeardown, test.fail));
	     
	     on wait(0.1) {
	    	 isConnected := true;
	    	 IDisposable connection := connectableObservable.connect();
	    	 on wait(0.5) {
	    		 isDisposed := true;
	    		 connection.dispose();
	    	 }
	     }
	}
	
	action makeSureNotCalledUntilConnected(any value) returns any {
		if not isConnected {
			test.fail("Should not be called until connected");
		}
		return value;
	}
	
	action onTeardown() {
		if isDisposed {
			test.complete();
	    } else {
	    	test.fail("Torn down without being disposed");
	    }
	}
}
