/* 
* Copyright (c) 2018 Software AG, Darmstadt, Germany and/or its licensors
*
* SPDX-License-Identifier: Apache-2.0
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

package utils;

using com.industry.rx_epl.IObservable;
using com.industry.rx_epl.Observable;
using com.industry.rx_epl.Subscriber;

event TestFailed {
  string message;
}
event TestComplete {}

event Test {
  string name;

  action complete() {
    send TestComplete() to name;
  }
  
  action fail(string message) {
    send TestFailed(message) to name;
  }
}

event TestObservables {
  static action createSync() returns IObservable {
    return Observable.fromValues([0.0, 1.0, 2.0, 3.0, 4.0, 5.0, 6.0, 7.0, 8.0, 9.0]);
  }
  
  static action createAsync() returns IObservable {
    return Observable.interval(0.01).map(intToFloat).take(10);
  }
  
  static action intToFloat(any intValue) returns any {
	  return (<integer> intValue).toFloat();
  }
}

event ExpectValues {
  sequence<any> expectedValues;
  action<> pass;
  action<string> fail;
  boolean isComplete;
  boolean noComplete;

  static action create(sequence<any> expectedValues, action<> pass, action<string> fail) returns Subscriber {
    ExpectValues evs := ExpectValues(expectedValues, pass, fail, false, false);
    Subscriber s := Subscriber.create().onNext(evs.next).onError(evs.error).onComplete(evs.complete);
    s.onUnsubscribe(evs.teardown);
    return s;
  }

  static action createNoComplete(sequence<any> expectedValues, action<> pass, action<string> fail) returns Subscriber {
    ExpectValues evs := ExpectValues(expectedValues, pass, fail, false, true);
    Subscriber s := Subscriber.create().onNext(evs.next).onError(evs.error).onComplete(evs.complete);
    s.onUnsubscribe(evs.teardown);
    return s;
  }
  
  static action createNoTeardown(sequence<any> expectedValues, action<> pass, action<string> fail) returns Subscriber {
    ExpectValues evs := ExpectValues(expectedValues, pass, fail, false, false);
    Subscriber s := Subscriber.create().onNext(evs.next).onError(evs.error).onComplete(evs.completeNoTeardown);
    s.onUnsubscribe(evs.doNotTeardown);
    return s;
  }
  
  action next(any value) {
    log "Next Called: " + value.toString();
    if expectedValues.size() = 0 {
        fail("Unexpected value received: " + value.toString());
    } else if not equalOrStringEqual(value, expectedValues[0]) {
    	fail("Value: " + value.toString() + " did not match expected: " + expectedValues[0].toString());
    } else {
    	expectedValues.remove(0);
    }
  }
  
  static action equalOrStringEqual(any value1, any value2) returns boolean {
	  try {
		  return value1 = value2;
	  } catch(com.apama.exceptions.Exception e) {
		  return value1.toString() = value2.toString();
	  }
  }
  
  action error(any e) {
    log "Error Called: " + e.toString();
    fail("Received Error: " + e.valueToString());
  }
  
  action complete() {
    log "Complete Called";
    if isComplete xor noComplete {
        fail("Unexpected complete received");
    }
    if expectedValues.size() = 0 {
      isComplete := true;
      // pass as long as teardown is called
    } else {
        fail("Did not receive expected values: " + expectedValues.toString());
    }
  }
  
  action completeNoTeardown() {
    log "Complete Called";
    if isComplete {
        fail("Unexpected complete received");
    }
    if expectedValues.size() = 0 {
        isComplete := true;
        pass();
      } else {
          fail("Did not receive expected values: " + expectedValues.toString());
      }
  }
  
  action teardown() {
    log "Teardown called";
    if isComplete or noComplete {
      pass();
    } else {
      fail("Unexpected teardown");
    }
  }
  
  action doNotTeardown() {
      fail("Should not have called teardown");
  }
}